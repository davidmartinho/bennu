package pt.ist.bennu.core.domain;

/*
 * @(#)bennu-core.dml
 *
 * Copyright 2009 Instituto Superior Tecnico
 * Founding Authors: Jo√£o Figueiredo, Luis Cruz, Paulo Abrantes, Susana Fernandes
 * 
 *      https://fenix-ashes.ist.utl.pt/
 * 
 *   This file is part of the Bennu web application infrastructure.
 *
 *   Bennu is free software: you can redistribute it and/or modify
 *   it under the terms of the GNU Lesser General Public License as published
 *   by the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.*
 *
 *   Bennu is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *   GNU Lesser General Public License for more details.
 *
 *   You should have received a copy of the GNU Lesser General Public License
 *   along with Bennu. If not, see <http://www.gnu.org/licenses/>.
 * 
 */

valueType pt.utl.ist.fenix.tools.util.i18n.MultiLanguageString as MultiLanguageString {
    externalizeWith {
        String exportAsString();
    }

    internalizeWith importFromString();
}

class Bennu {
}

class User {
	String username  (REQUIRED);
	String password;
	DateTime created;
	DateTime lastLogoutDateTime;
	DateTime expiration;
	String email;
}

relation SystemUsers {
	Bennu playsRole bennu {
		multiplicity 1..1;
	}
	User playsRole users {
		multiplicity *;
	}
}

/* Authorization Groups */

class groups.PersistentGroup {
}

relation PersistentGroups {
	Bennu playsRole bennu {
		multiplicity 1..1;
	}
	groups.PersistentGroup playsRole groups {
		multiplicity *;
	}
}

relation SystemGroups {
	Bennu playsRole system {
		multiplicity 0..1;
	}
	groups.PersistentGroup playsRole systemGroups {
		multiplicity *;
	}
}

class groups.NobodyGroup extends groups.PersistentGroup {
}

class groups.AnonymousGroup extends groups.PersistentGroup {
}

class groups.AnyoneGroup extends groups.PersistentGroup {
}

class groups.LoggedGroup extends groups.PersistentGroup {
}

class groups.PeopleGroup extends groups.PersistentGroup {
}

relation PeopleGroupMembers {
	groups.PeopleGroup playsRole {
		multiplicity *;
	}
	User playsRole member {
		multiplicity *;
	}
}

class groups.CompositionGroup extends groups.PersistentGroup {
}

relation PersistentGroupUnionGroup {
	groups.PersistentGroup playsRole children {
		multiplicity 1..*;
	}
	groups.CompositionGroup playsRole {
		multiplicity *;
	}
}

class groups.UnionGroup extends groups.CompositionGroup {
}

class groups.IntersectionGroup extends groups.CompositionGroup {
}

class groups.DifferenceGroup extends groups.CompositionGroup {
}

class groups.NegationGroup extends groups.PersistentGroup {
}

relation PersistentGroupNegationGroup {
	groups.PersistentGroup playsRole negated {
		multiplicity 1..1;
	}
	groups.NegationGroup playsRole {
		multiplicity *;
	}
}

/* Password Recovery */
class PasswordRecoveryRequest {
	DateTime requested;
	String verificationHash;
	DateTime consumed;
}

relation UserPasswordRecoveryRequest {
	User playsRole user;
	PasswordRecoveryRequest playsRole passwordRecoveryRequest;
}

relation PasswordRecoveryRequestPasswordRecoveryRequest {
	PasswordRecoveryRequest playsRole previous;
	PasswordRecoveryRequest playsRole next;
}

/* Virtual Hosting */
class VirtualHost {
	String hostname;
	MultiLanguageString applicationTitle;
	MultiLanguageString htmlTitle;
	MultiLanguageString applicationSubTitle;
	MultiLanguageString applicationCopyright;
	String supportEmailAddress;
	String systemEmailAddress;
	String supportedLanguages;
}

relation SystemHosts {
	Bennu playsRole bennu {
		multiplicity 1..1;
	}
	VirtualHost playsRole virtualHosts {
		multiplicity *;
	}
}
